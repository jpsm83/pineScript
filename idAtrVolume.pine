// confirmation small time frame

//@version=5
indicator("Joao 2023 indicator for confirmation small time frame", overlay = true)

// get accumulation/distribution
accDist = ta.accdist
accDistMomentunVolume = accDist > accDist[1] and accDist[1] > accDist[2]

// get vwap
vwap = ta.vwap(source = close)
plot(series = vwap, title = "vwap", color = color.new(color = color.white, transp = 60), linewidth = 2)

// get current emas
ema9 = ta.ema(source = close, length = 9)
plot(series = ema9, title = "ema9", color = color.new(color = color.green, transp = 60), linewidth = 1)
ema21 = ta.ema(source = close, length = 21)
plot(series = ema21, title = "ema21", color = color.new(color = color.yellow, transp = 60), linewidth = 1)
ema50 = ta.ema(source = close, length = 50)
plot(series = ema50, title = "ema50", color = color.new(color = color.red, transp = 60), linewidth = 1)
ema200 = ta.ema(source = close, length = 200)
plot(series = ema200, title = "ema200", color = color.new(color = color.orange, transp = 0), linewidth = 2)

// offset distance from current ema200
bullishOffsetEma50 = ta.ema(high, 50) + 0.1*(ta.ema(high, 50) - ema50)
bearishOffsetEma50 = ta.ema(low, 50) - 0.1*(ema50 - ta.ema(low, 50))

// ==================================================================================================================================================================================================
// confirmation =============================================================================================================================================================
// ==================================================================================================================================================================================================

// price position open and close
CONFIRMATION_BullishCrossover = ta.crossover(source1 = ema9, source2 = ema21) and close > ema200 and open > ema200 and close > vwap
CONFIRMATION_BearishCrossover = ta.crossover(source1 = ema21, source2 = ema9) and close < ema200 and open < ema200 and close < vwap

// distance from ema21 to ema50 band
CONFIRMATION_BullishBand = close > open and close > bullishOffsetEma50
CONFIRMATION_BearishBand = close < open and close < bearishOffsetEma50

// confirmation setup
CONFIRMATION_Bullish = CONFIRMATION_BullishCrossover and CONFIRMATION_BullishBand and accDistMomentunVolume
CONFIRMATION_Bearish = CONFIRMATION_BearishCrossover and CONFIRMATION_BearishBand and accDistMomentunVolume

// draw data to chart
plotshape(CONFIRMATION_Bullish, style=shape.arrowup, color=color.green, location=location.belowbar, size = size.huge, text = "CONFIRMATION", textcolor = color.green)
plotshape(CONFIRMATION_Bearish, style=shape.arrowdown, color=color.green, location=location.abovebar, size = size.huge, text = "CONFIRMATION", textcolor = color.green)

// send alert
repaintingDesable = false
if (CONFIRMATION_Bullish or CONFIRMATION_Bearish) and barstate.isconfirmed
    repaintingDesable := true

alertcondition(repaintingDesable, title = "Joao 2023 indicator for confirmation small time frame", message = "Confirmation at {{ticker}}")